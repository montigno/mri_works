  macro "Profil T2 Tool - C059o11ee" {

      max_magn = 0.0;

      leftButton=16;
      rightButton=4;
      shift=1;
      ctrl=2; 
      alt=8;
      x2=-1; y2=-1; z2=-1; flags2=-1;

      selectWindow(img1);
      Stack.setFrame(1)
      Stack.getDimensions(width, height, channels, slices, frames);
      run("Properties...", "channels="+channels+" slices="+slices+" frames="+frames+" pixel_width=1.0000000 pixel_height=1.0000000 voxel_depth=1.0000000");
      values = newArray(frames);
      values2 = newArray(frames);
      fittings = newArray(frames);   

      getCursorLoc(x, y, z, flags);
      while (flags&leftButton!=0) {
          getCursorLoc(x, y, z, flags);
          if (x!=x2 || y!=y2 || z!=z2 || flags!=flags2) {

            makePoint(x, y, "small red cross");

            selectWindow(img2);
            run("Select None");
            setSlice(z+1);
            T2 = getPixel(x, y);
            makePoint(x, y, "small blue cross");

            selectWindow(img3);
            run("Select None");
            setSlice(z+1);
            M0 = getPixel(x, y);
            makePoint(x, y, "small blue cross");

            selectWindow(img4);
            run("Select None");
            setSlice(z+1);
            C = getPixel(x, y);
            makePoint(x, y, "small blue cross");

            selectWindow(img1);

            for (i=0;i < frames; i++){
                //Stack.setFrame(i+1);
                setZCoordinate((slices * i) + z);
                values[i] = getPixel(x, y);
                values2[i] = M0*exp(-Times[i] / T2)+C;
            }

            if (max_magn < values[0]){
                max_magn = values[0] * 1.2;
            }
            if (max_magn < values2[0]){
                max_magn = values2[0] * 1.2;
            }

            Plot.create("Profile T2", "TE (ms)", "M0 (amp.)");
            Plot.setFrameSize(600, 400);
            Plot.setLimits(0, Times[frames-1]*1.1, 0, max_magn);
            Plot.setColor("red");
            Plot.setLineWidth(2);
            Plot.add("x", Times, values);
            Plot.setColor("blue");
            Plot.setLineWidth(2);
            Plot.add("line", Times, values2);
            Plot.addText("s(t)=M0.e(-t/T2))+C",0.7, 0.2);
            Plot.addText("T2 = "+T2, 0.7, 0.3);
            Plot.addText("M0 = "+M0, 0.7, 0.35);
            Plot.addText("C = "+C, 0.7, 0.4);
            Plot.update();

            selectWindow(img1);
            Stack.setFrame(1)

            logOpened = true;
            startTime = getTime();
          }
          x2=x; y2=y; z2=z; flags2=flags;
          wait(10);
      }
 }
